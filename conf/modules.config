/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    publishDir = [
        path:  { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode:  params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]

    withName: SAMPLESHEET_CHECK {
        publishDir = [
            path:   { "${params.outdir}/pipeline_info" },
            mode:   params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: CUSTOM_DUMPSOFTWAREVERSIONS {
        publishDir = [
            path:    { "${params.outdir}/pipeline_info" },
            mode:    params.publish_dir_mode,
            pattern: '*_versions.yml'
        ]
    }

    withName: "FORCE_GENERATE_ANALYSIS_MASK" {
        memory = { 500.MB * task.attempt }
        time   = { 20.minute * task.attempt }
        publishDir = [
            path:    { "${params.outdir}/preparation/" },
            mode:    params.publish_dir_mode,
            pattern: '**.tif'
        ]
    }

    withName: "FORCE_GENERATE_TILE_ALLOW_LIST" {
        memory = { 500.MB * task.attempt }
        time   = { 20.minute * task.attempt }
        publishDir = [
            path:    { "${params.outdir}/preparation/" },
            pattern: '*.txt',
            mode:    params.publish_dir_mode
        ]
    }

    withName: "FORCE_PREPROCESS" {
        cpus          = 4
        memory        = { 4500.MB * task.attempt }
        time          = { 2.h * task.attempt }
        errorStrategy = 'retry'
        maxRetries    = 5
        publishDir    = [
            [
                path:    { "${params.outdir}/preprocess/${task.tag}/logs" },
                mode:    params.publish_dir_mode,
                pattern: '*.log'
            ],
            [
                path:    { "${params.outdir}/preprocess/${task.tag}" },
                mode:    'symlink',
                pattern: 'level2_ard/**/*'
            ]
        ]
    }

    withName: "MERGE_BOA" {
        memory = { 2000.MB * task.attempt }
        time   = { 2.h * task.attempt }
    }

    withName: "MERGE_QAI" {
        memory = { 2000.MB * task.attempt }
        time   = { 2.h * task.attempt }
    }

    withName: "HIGHER_LEVEL_CONFIG" {
        errorStrategy = 'retry'
        maxRetries    = 5
        publishDir    = [
            path:    { "${params.outdir}/higher-level/${task.tag}/param_files" },
            mode:    params.publish_dir_mode,
            pattern: '*.prm'
        ]
    }

    withName: "FORCE_HIGHER_LEVEL" {
        cpus   = 6
        memory = { (params.only_tile ? 12000.MB : 3300.MB ) * task.attempt }
        time   = { 2.h * task.attempt }
        publishDir = [
            path:    { "${params.outdir}/higher-level/${task.tag}" },
            mode:    'symlink',
            pattern: 'trend/*.tif',
            saveAs:  { "trend_files/${it.tokenize('/')[-1]}" }
        ]
    }

    withName: "FORCE_PYRAMID" {
        memory      = { 1500.MB * task.attempt }
        time        = { 20.minute * task.attempt }
        stageInMode = 'copy'
        publishDir  = [
            path:    "${params.outdir}/trend/pyramid/",
            saveAs:  { "${it.substring(12,it.indexOf("."))}/trend/${it.substring(0,11)}/$it" },
            pattern: '*.tif*',
            mode:    params.publish_dir_mode
        ]
    }

    withName: "FORCE_MOSAIC" {
        memory     = { 1500.MB * task.attempt }
        time       = { 20.minute * task.attempt }
        publishDir = [
            path:     "${params.outdir}/trend/mosaic/",
            mode:     params.publish_dir_mode,
            override: false
        ]
    }

    withName: "CHECK_RESULTS" {
        errorStrategy = { task.exitStatus == 143 ? 'retry' : 'ignore' }
        memory        = { 14000.MB * task.attempt }
        time          = { 2.h * task.attempt }
        publishDir    = [
            enabled: false
        ]
    }

    withName: "PREPROCESS_CONFIG" {
        errorStrategy = 'retry'
        maxRetries    = 5
        publishDir    = [
            path:    { "${params.outdir}/preprocess/${task.tag}/param_files" },
            mode:    params.publish_dir_mode,
            pattern: '*.prm'
        ]
    }

    withName: 'MULTIQC' {
        ext.args   = params.multiqc_title ? "--title \"$params.multiqc_title\"" : ''
        publishDir = [
            path:   { "${params.outdir}/multiqc" },
            mode:   params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: "UNTAR_*" {
        publishDir = [
            path:   { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
            mode:   'symlink',
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

}
